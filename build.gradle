import org.jsonschema2pojo.gradle.GenerateJsonSchemaJavaTask

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'org.jsonschema2pojo:jsonschema2pojo-gradle-plugin:1.0.2'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'eclipse'
    id 'maven-publish'
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'io.codearte.nexus-staging' version '0.11.0'
}

// Calling 'apply' here because jsonschema2pojo doesn't exist in Gradle Central Plugin Repository
apply plugin: 'jsonschema2pojo'
tasks.getByName("generateJsonSchema2Pojo").enabled(false) // Disable default jsonSchema2Pojo task

archivesBaseName = 'alpaca-java'
group = 'io.github.mainstringargs'
version = '5.0.13-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'com.google.guava:guava:20.0'
    compile "com.mashape.unirest:unirest-java:1.4.9"
    compile "com.google.code.gson:gson:2.8.5"
    compile "commons-lang:commons-lang:2.6"
    compile 'org.apache.logging.log4j:log4j-api:2.11.1'
    compile 'org.apache.logging.log4j:log4j-core:2.11.1'
    compile "org.eclipse.jetty.websocket:javax-websocket-client-impl:9.4.28.v20200408"

    testCompile 'junit:junit:4.12'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

// Used to suppress java doc warnings
javadoc.options.addStringOption('Xdoclint:none', '-quiet')
javadoc.options.addStringOption("charset", "UTF-8")

// Publish to local maven repo for local testing
build {
    finalizedBy publishToMavenLocal
}

//mainClassName = 'io.github.mainstringargs.polygon.PolygonExample'
mainClassName = 'io.github.mainstringargs.alpaca.AlpacaExample'

//
// START publishing
//
publishing {
    publications {
        mainPublication(MavenPublication) {
            from components.java

            pom {
                name = 'alpaca-java'
                description = 'Java API for Alpaca and Polygon'
                url = 'https://github.com/Petersoj/alpaca-java'
                inceptionYear = '2018'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'mainstringargs'
                        name = 'main(String[] args)'
                        email = 'mainstringargs@github.io'
                    }

                    developer {
                        id = 'Petersoj'
                        name = 'Jacob Peterson'
                    }
                }

                scm {
                    url = "https://github.com/Petersoj/alpaca-java.git"
                    connection = "scm:git:https://github.com/Petersoj/alpaca-java.git"
                    developerConnection = "scm:git:git@github.com/Petersoj/alpaca-java.git"
                }
            }
        }
    }
}

extraArchive {
    sources = true
    tests = true
    javadoc = true
}
nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}

nexusStaging {
    packageGroup = "io.github.mainstringargs" //optional if packageGroup == project.getGroup()
}

// Used to prevent accidental publication of Alpaca keys
processResources {
    exclude('alpaca.properties', 'polygon.properties')
}
//
// END publishing
//

//
// START POJO generation
//
// The generated POJOs will be in a package structure analogous to the path in the 'schema_json/' directory
// See: https://github.com/joelittlejohn/jsonschema2pojo/wiki/Reference
task generatePOJOs() {
    final def targetDirectory = "${project.buildDir}" + File.separator + "generated-sources" + File.separator + "schemajson"
    final def jsonSourceDirectory = "${project.projectDir}" + File.separator + "schema_json" + File.separator
    final def jsonPackageNameStart = "io.github.mainstringargs.domain."

    // Loop through all files in schema JSON file tree
    file(jsonSourceDirectory).eachFileRecurse { jsonFile ->
        if (jsonFile.getName().endsWith(".json")) {
            def startPackageIndex = jsonFile.getAbsolutePath().indexOf(jsonSourceDirectory) + jsonSourceDirectory.length()
            def targetPackage = jsonPackageNameStart + jsonFile.getParentFile().getAbsolutePath()
                    .substring(startPackageIndex)
                    .replace(File.separator, '.').replace('-', '').replace('_', '')
                    .toLowerCase()

            def jsonToPOJOTask = tasks.create('json2POJOTask-' + targetPackage + "." + jsonFile.getName(),
                    GenerateJsonSchemaJavaTask)

            jsonToPOJOTask.doFirst {
                jsonToPOJOTask.configuration.source = files(jsonFile.getAbsolutePath())
                jsonToPOJOTask.configuration.targetDirectory = file(targetDirectory)
                jsonToPOJOTask.configuration.targetPackage = targetPackage
            }

            dependsOn jsonToPOJOTask
        }
    }
}

compileJava {
    dependsOn generatePOJOs
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'build/generated-sources/schemajson'
        }
    }
}

jsonSchema2Pojo {
    includeAdditionalProperties = true

    targetDirectory = file('build/generated-sources/schemajson')

    propertyWordDelimiters = ['-', '_'] as char[]

    annotationStyle = 'gson'

    sourceType = 'jsonschema'

    customDateTimePattern = "yyyy-MM-ddTHH:mm:ssZ"

    includeConstructors = true

    serializable = true

    includeGetters = true

    includeSetters = true
}
//
// END POJO generation
//
